plugins {
    id 'java'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.7.3'
    id 'com.github.hierynomus.license' version '0.14.0'
    id 'eclipse'
}

version = '1.80'

defaultTasks 'jar'

// Prevent warning about POM relocation not being fully supported
configurations.all {
    resolutionStrategy {
        force 'xml-apis:xml-apis:1.4.01'
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven {
	// Added for stuff that is not in jcenter yet.
	url 'https://dl.bintray.com/cerieljacobs/JungleComputing'
    }
}

dependencies {
    compile group: 'nl.junglecomputing', name: 'touchgraph', version: '1.58'
    compile group: 'org.apache.sshd', name: 'sshd-core', version: '1.7.0' 
    compile group: 'nl.junglecomputing', name: 'sbbi-upnplib', version: '1.0.4'
    compile group: 'de.javawi.jstun', name: 'jstun', version: '0.7.4'
    compile group: 'com.google.guava', name: 'guava', version: '19.0'
    compile group: 'commons-logging', name: 'commons-logging', version: '1.2'
    compile group: 'commons-jxpath', name: 'commons-jxpath', version: '1.1'
    compile group: 'log4j', name: 'log4j', version: '1.2.13'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.21'
    testCompile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.21'
}

def pomConfig = {
    scm {
        url "https://github.com/junglecomputing/smartsockets.git"
    }
}

license {
    ext.copyright_year = 2010
    ext.copyright_owner = 'Vrije Universiteit'
    header rootProject.file('gradle/HEADER')
    strictCheck true
    excludes(["**/log4j.properties"])
    mapping {
        // IntelliJ IDEA gives "Dangling Javadoc comment." warning when default JAVADOC_STYLE is used,
        // so switch to comment style
        java = 'SLASHSTAR_STYLE'
    }
}

licenseFormat.description = "Applies the license found in the header file in files missing the header"

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
	    artifact sourcesJar {
		classifier "sources"
	    }
	    artifact javadocJar {
		classifier "javadoc"
	    }
	    groupId 'nl.junglecomputing'
	    artifactId 'smartsockets'
	    version version

	    pom.withXml {
                def root = asNode()
                root.appendNode('url', 'https://github.com/junglecomputing/smartsockets.git')
                root.children().last() + pomConfig
            }
        }
    }
}

tasks.withType(Jar) {
    destinationDir = file("$rootDir/lib")
}

task copyRuntimeLibs(type: Copy) {
    into "lib"
    from configurations.runtime
}

bintray {
    user = System.getenv('BINTRAY_NAME')
    key = System.getenv('BINTRAY_APIKEY')
    publications = ['mavenJava']
    pkg {
	repo = 'JungleComputing'
	name = 'smartsockets'
	userOrg = "$user"
	vcsUrl = 'https://github.com/junglecomputing/smartsockets.git'
	licenses = ['Apache-2.0']
    }
}
